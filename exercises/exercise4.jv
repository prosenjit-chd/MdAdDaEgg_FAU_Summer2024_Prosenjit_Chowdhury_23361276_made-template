  
  pipeline WeatherPipeline {
  // Extracts weather data from a specified URL
  block WeatherExtractor oftype HttpExtractor {
    url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
  }

  // Interprets the downloaded file as a ZIP archive

  block ZipInterpreter oftype ArchiveInterpreter {
    archiveType: "zip";
  }

  // Picks a specific file from the extracted contents


  block WeatherFilePicker oftype FilePicker {
    path: "/data.csv";
  }

  // Interprets the chosen file as a CSV file
  
  block WeatherCSVInterpreter oftype CSVInterpreter {
    delimiter: ';';
  }

  // Interprets the chosen file as a plain text file
  
  block TextInterpreter oftype TextFileInterpreter {}

  // Deletes specified columns from the data
  
  block ColDeleter oftype ColumnDeleter {
    delete: [column F, column G, column H, column I];
  }

  // Selects a range of cells from the data
 
  block ColDataSelector oftype CellRangeSelector {
    select: range A1:K*;
  }

  // Data validation for other columns
    constraint PositiveRange oftype RangeConstraint { lowerBound: 0; lowerBoundInclusive: false;}
    valuetype PositiveInteger oftype integer { constraints: [ PositiveRange ]; }

    // Converts Tempareture Sheet to Table and dropped unnecessary columns and rename headers (In: Sheet, Out: Table)
  block WeatherTableInterpreter oftype TableInterpreter {
    header: false;
    columns: [
      "id" oftype PositiveInteger,
      "producer" oftype text,
      "model" oftype text,
      "month" oftype integer,
      "temperature" oftype decimal,
      "battery_temperature" oftype decimal
    ];
  }

  // Transformation definition to convert Celsius to Fahrenheit
  
  transform TempConvert {
    from tempCelsius oftype decimal;
    to tempFahrenheit oftype decimal;

    // Conversion formula
    tempFahrenheit: (tempCelsius * 9/5) + 32;
  }

  // Applies the temperature conversion transformation to the 'Temperatur' column
  block TransformerA oftype TableTransformer {
    inputColumns: ['temperature'];
    outputColumn: 'temperature';
    use: TempConvert;  // Using the defined transformation
  }

  // Applies the temperature conversion transformation to the 'Batterietemperatur' column
  block TransformerB oftype TableTransformer {
    inputColumns: ['battery_temperature'];
    outputColumn: 'battery_temperature';
    use: TempConvert;  // Using the defined transformation
  }

  // Loads the processed data into an SQLite table
  block WeatherLoader oftype SQLiteLoader {
    table: "temperatures";
    file: "./temperatures.sqlite";
    dropTable: false;
  }



  // Build a chain of all the TempareturePipeline's blocks
  WeatherExtractor
  -> ZipInterpreter
  -> WeatherFilePicker
  -> TextInterpreter
  -> WeatherCSVInterpreter
  -> ColDataSelector
  -> ColDeleter
  -> WeatherTableInterpreter
  -> TransformerA
  -> TransformerB
  -> WeatherLoader;
}